OUTPUT_ARCH(mips)
ENTRY(BootEntry)
MEMORY
{
    ram (wx) : ORIGIN = 0x80000000, LENGTH = 64M
}

SECTIONS
{
	/**** Code and read-only data ****/
	.text :
	{
		_ftext = ABSOLUTE(.) ;	/* Start of code and read-only data	*/
	    *(.text*)
	    *(.gnu.linkonce.*)
	    *(__libc*)
	    *(.rel.dyn)
		_ecode = ABSOLUTE(.) ;	/* End of code				*/
		
		*(.rodata*)
		
		. = ALIGN(8);
		_etext = ABSOLUTE(.);	/* End of code and read-only data	*/
	} > ram
	
	.mdata :
	{
		_start_mplayer_data = ABSOLUTE(.);
		../../UsrLib/libout/JZ4760B/mplayer_lib.a:(.data)
		../../UsrLib/libout/JZ4760B/libavcodec.a:(.data)
		../../UsrLib/libout/JZ4760B/libavcore.a:(.data)
		../../UsrLib/libout/JZ4760B/libavformat.a:(.data)
		../../UsrLib/libout/JZ4760B/libswscale.a:(.data)
		../../UsrLib/libout/JZ4760B/libavutil.a:(.data)
		../../UsrLib/libout/JZ4760B/libpostproc.a:(.data)
		../../UsrLib/libout/JZ4760B/libmad.a:(.data)
		../../UsrLib/libout/JZ4760B/libh264.a:(.data)
		../../UsrLib/libout/JZ4760B/libmpeg4.a:(.data)
		../../UsrLib/libout/JZ4760B/librv9.a:(.data)
		../../UsrLib/libout/JZ4760B/libvc1.a:(.data)
		_end_mplayer_data = ABSOLUTE(.);
	} > ram
	
	/**** Initialised data ****/
	.data :
	{
		_fdata = ABSOLUTE(.);	/* Start of initialised data		*/
		*(.data*)
		*(.eh_frame*)
		*(.gcc_except_table*)
		
		. = ALIGN(8);
		__START_ctros = ABSOLUTE(.);
		KEEP(*(SORT(.ctors*)))
		__END_ctros = ABSOLUTE(.);
		__START_dtros = ABSOLUTE(.);
		KEEP(*(SORT(.dtors*)))
		__END_dtros = ABSOLUTE(.);
		
		_gp = ABSOLUTE(. + 0x7ff0); /* Base of small data			*/
		
		*(.lit8) 
		*(.lit4) 
		*(.sdata) 
		
		. = ALIGN(8);
		
		_edata  = ABSOLUTE(.);	/* End of initialised data		*/
	} > ram

	/**** Uninitialised data ****/
	_start_bss = .;			/* Start of uninitialised data		*/

	.msbss :
	{
		_start_mplayer_sbss = ABSOLUTE(.);
		../../UsrLib/libout/JZ4760B/mplayer_lib.a:(.sbss)
		../../UsrLib/libout/JZ4760B/libavcodec.a:(.sbss)
		../../UsrLib/libout/JZ4760B/libavcore.a:(.sbss)
		../../UsrLib/libout/JZ4760B/libavformat.a:(.sbss)
		../../UsrLib/libout/JZ4760B/libswscale.a:(.sbss)
		../../UsrLib/libout/JZ4760B/libavutil.a:(.sbss)
		../../UsrLib/libout/JZ4760B/libpostproc.a:(.sbss)
		../../UsrLib/libout/JZ4760B/libmad.a:(.sbss)
		../../UsrLib/libout/JZ4760B/libh264.a:(.sbss)
		../../UsrLib/libout/JZ4760B/libmpeg4.a:(.sbss)
		../../UsrLib/libout/JZ4760B/librv9.a:(.sbss)
		../../UsrLib/libout/JZ4760B/libvc1.a:(.sbss)
		../../UsrLib/libout/JZ4760B/spc.a:(.sbss)
		../../UsrLib/libout/JZ4760B/fipop.a:(.sbss)
		../../UsrLib/libout/JZ4760B/libmpeg2.a:(.sbss)

		../../UsrLib/libout/JZ4760B/mplayer_lib.a:(.scommon)
		../../UsrLib/libout/JZ4760B/libavcodec.a:(.scommon)
		../../UsrLib/libout/JZ4760B/libavcore.a:(.scommon)
		../../UsrLib/libout/JZ4760B/libavformat.a:(.scommon)
		../../UsrLib/libout/JZ4760B/libswscale.a:(.scommon)
		../../UsrLib/libout/JZ4760B/libavutil.a:(.scommon)
		../../UsrLib/libout/JZ4760B/libpostproc.a:(.scommon)
		../../UsrLib/libout/JZ4760B/libmad.a:(.scommon)
		../../UsrLib/libout/JZ4760B/libh264.a:(.scommon)
		../../UsrLib/libout/JZ4760B/libmpeg4.a:(.scommon)
		../../UsrLib/libout/JZ4760B/librv9.a:(.scommon)
		../../UsrLib/libout/JZ4760B/libvc1.a:(.scommon)
		../../UsrLib/libout/JZ4760B/spc.a:(.scommon)
		../../UsrLib/libout/JZ4760B/fipop.a:(.scommon)
		../../UsrLib/libout/JZ4760B/libmpeg2.a:(.scommon)
		_end_mplayer_sbss = ABSOLUTE(.);
	}

	.sbss : 
	{ 
		*(.sbss) 
		*(.scommon)
	}

	.mbss :
	{
		_start_mplayer_bss = ABSOLUTE(.);
		../../UsrLib/libout/JZ4760B/mplayer_lib.a:(.bss)
		../../UsrLib/libout/JZ4760B/libavcodec.a:(.bss)
		../../UsrLib/libout/JZ4760B/libavcore.a:(.bss)
		../../UsrLib/libout/JZ4760B/libavformat.a:(.bss)
		../../UsrLib/libout/JZ4760B/libswscale.a:(.bss)
		../../UsrLib/libout/JZ4760B/libavutil.a:(.bss)
		../../UsrLib/libout/JZ4760B/libpostproc.a:(.bss)
		../../UsrLib/libout/JZ4760B/libmad.a:(.bss)
		../../UsrLib/libout/JZ4760B/libh264.a:(.bss)
		../../UsrLib/libout/JZ4760B/libmpeg4.a:(.bss)
		../../UsrLib/libout/JZ4760B/librv9.a:(.bss)
		../../UsrLib/libout/JZ4760B/libvc1.a:(.bss)
		../../UsrLib/libout/JZ4760B/spc.a:(.bss)
		../../UsrLib/libout/JZ4760B/fipop.a:(.bss)
		../../UsrLib/libout/JZ4760B/libmpeg2.a:(.bss)

		../../UsrLib/libout/JZ4760B/mplayer_lib.a:(COMMON)
		../../UsrLib/libout/JZ4760B/libavcodec.a:(COMMON)
		../../UsrLib/libout/JZ4760B/libavcore.a:(COMMON)
		../../UsrLib/libout/JZ4760B/libavformat.a:(COMMON)
		../../UsrLib/libout/JZ4760B/libswscale.a:(COMMON)
		../../UsrLib/libout/JZ4760B/libavutil.a:(COMMON)
		../../UsrLib/libout/JZ4760B/libpostproc.a:(COMMON)
		../../UsrLib/libout/JZ4760B/libmad.a:(COMMON)
		../../UsrLib/libout/JZ4760B/libh264.a:(COMMON)
		../../UsrLib/libout/JZ4760B/libmpeg4.a:(COMMON)
		../../UsrLib/libout/JZ4760B/librv9.a:(COMMON)
		../../UsrLib/libout/JZ4760B/libvc1.a:(COMMON)
		../../UsrLib/libout/JZ4760B/spc.a:(COMMON)
		../../UsrLib/libout/JZ4760B/fipop.a:(COMMON)
		../../UsrLib/libout/JZ4760B/libmpeg2.a:(COMMON)
		_end_mplayer_bss = ABSOLUTE(.);
	}
	
	.bss :
	{
		*(.bss)
		*(COMMON)
		. = ALIGN(8) ;
		_freemem = .;
	}
	_end_bss = . ;			/* End of unitialised data		*/

	.gptab.sdata : 
	{ 
		*(.gptab.data)
		*(.gptab.sdata) 
	}
	.gptab.sbss : 
	{ 
		*(.gptab.bss)
		*(.gptab.sbss)
	}
	
	/DISCARD/ :
	{
		*(.reginfo)
	}
	
	PROVIDE(etext = _etext);
	PROVIDE (edata = .);
	PROVIDE (end = .);
}


